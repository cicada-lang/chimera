// "A multiset approach to arithmetic"
// https://www.youtube.com/watch?v=4xoF2SRp194

clause Multiset([])
clause Multiset([car | cdr]) -- { Multiset(car) Multiset(cdr) }

// find x limit 10 {
//   Multiset(x)
// }

clause Zero([])

clause Nat([])
clause Nat([car | cdr]) -- { Zero(car) Nat(cdr) }

// find x limit 10 {
//   Nat(x)
// }

clause Poly([])
clause Poly([car | cdr]) -- { Nat(car) Poly(cdr) }

// find x limit 10 {
//   Poly(x)
// }

clause Multi([])
clause Multi([car | cdr]) -- { Poly(car) Multi(cdr) }

// find x limit 10 {
//   Multi(x)
// }

clause Append([], t, t)
clause Append([a | d], t, out) -- {
  out = [a | res]
  Append(d, t, res)
}

clause Add(x, y, z) -- { Append(x, y, z) }

clause AddMany([], [])
clause AddMany([x], x)
clause AddMany([x, y | rest], out) -- {
  Add(x, y, z)
  AddMany([z | rest], out)
}

// find [x, y, out] limit 10 {
//   Nat(x)
//   Nat(y)
//   Add(x, y, out)
// }

// find [xs, out] limit 20 {
//   Poly(xs)
//   AddMany(xs, out)
// }

clause Product([], _, [])
clause Product([_x | _xs], [], [])
clause Product([x | xs], [y | ys], out) -- {
  Product([x], ys, xSide)
  Product(xs, [y], ySide)
  Product(xs, ys, body)
  Append(xSide, ySide, sides)
  Append(sides, body, tmp)
  out = [[x, y] | tmp]
}

find q {
  Product([1], ["a"], q)
}

find q {
  Product([1, 2], ["a", "b"], q)
}

find q {
  Product([1, 2, 3], ["a", "b", "c"], q)
}

clause AddPairs([], [])
clause AddPairs([[x, y] | ps], [z | res]) -- {
  Add(x, y, z)
  AddPairs(ps, res)
}

clause Mul(x, y, z) -- {
  Product(x, y, pairs)
  AddPairs(pairs, z)
}

find z {
  Mul([[], []], [[], []], z)
}

find [x, y] limit 3 {
  Mul(x, y, [[], [], [], []])
}

find [x, y, z] limit 10 {
  Nat(x)
  Nat(y)
  Mul(x, y, z)
}

clause MulMany([], [])
clause MulMany([x], x)
clause MulMany([x, y | rest], out) -- {
  Mul(x, y, z)
  MulMany([z | rest], out)
}

find z {
  MulMany([[[], []], [[], []], [[], []]], z)
}

find z {
  MulMany([[[], []], [[]]], z)
}

find z {
  MulMany([[[]], [[], []]], z)
}

find z {
  MulMany([[[], []]], z)
}

find [x, y] limit 2 {
  Nat(x)
  Nat(y)
  Mul(x, y, [[], []])
}

find [xs, z] limit 20 {
  Poly(xs)
  MulMany(xs, z)
}
