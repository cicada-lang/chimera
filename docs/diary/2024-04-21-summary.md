---
title: 总结 / Summary
author: Xie Yuheng
date: 2024-04-21
---

Chimera 是一个大杂烩，其中中包含了多个语言。

# 逻辑式语言

用 `clause` 来定义关系，约定关系的名字是 PascalCase。

```chimera
clause Append([], l, l)
clause Append([a | d], l, [a | r]) -- {
  Append(d, l, r)
}
```

用 `find` 来写 query。

```chimera
print find [x, y] {
  Append(x, y, [1, 2, 3])
}
```

关系的参数可以是 term，约定 term 的 head 必须是 camelCase。

```chimera
clause Exp(var(name)) -- { String(name) }
clause Exp(fn(name, ret)) -- { String(name) Exp(ret) }
clause Exp(ap(target, arg)) -- { Exp(target) Exp(arg) }
```

# term rewrite

用 `rule` 定义 term rewrite 规则。

```chimera
rule conjunctiveNormalForm {
  not(not(x)) => quote x
  not(and(x, y)) => quote or(not(x), not(y))
  not(or(x, y)) => quote and(not(x), not(y))
  or(and(x, y), z) => quote and(or(x, z), or(y, z))
  or(x, and(y, z)) => quote and(or(x, y), or(x, z))
}
```

用内置函数 `rewrite` 来运行 rewrite 规则。

```chimera
print rewrite(
  conjunctiveNormalForm,
  quote not(and(not(A), not(B)))
)
```

用 `quote` 来构造 term，
`quote` 内可以用 `eval` 来运行代码返回 `term`。

改进方案：

- 可能应该用 `@rewrite` 来命名内置函数。
- 可能应该用 `@print` 内置函数，而不应该用 `print` statement。
  - 应该想办法把顶层的 exp 视为是 计算，应该避免 `compute` 这个 statement。

# hypergraph rewrite

可以用 `hyperrule` 定义 hypergraph rewrite 规则。

目的是实现可扩展的 constraint programming，
但是我们对 constraint programming 的理解还不够，
这里的设计并不好。

对 constraint programming 的正确理解是，
每个变量都有可化简的 domain，
而我们这次的设计中没有体现出来这一点。

# 无类型过程式语言

注意，能够使用内置函数 `rewrite`，
在于我们有一个过程式语言来把各个语言串联起来。

用 `function` 定义函数。

```chimera
function id(x) {
  return x
}
```

# 问题

我们应该如何改进 Chimera 当前的设计，
使得它能成为一个有用的语言？

首先我认为它不好用在于它没有类型系统。
但是也许无类型就是它的特点，
因为无类型的 term 很适合逻辑式与 term rewrite。

也许我们应该用 tagged term，
并且用 datatype 来限制 term 的形式：

```chimera todo
datatype Exp {
  Var(name: String)
  Fn(name: String, ret: Exp)
  Ap(target: Exp, arg: Exp)
}

Exp::Ap(Exp::Var("f"), Exp::Var("x"))
```

在 datatype 的定义之后 `Exp` 可以被视为 type constraint。

这样就不用 quote 了。

```chimera todo
rule conjunctiveNormalForm {
  Logic::Not(Logic::Not(x)) => x
  Logic::Not(Logic::And(x, y)) => Logic::Or(Logic::Not(x), Logic::Not(y))
  Logic::Not(Logic::Or(x, y)) => Logic::And(Logic::Not(x), Logic::Not(y))
  Logic::Or(Logic::And(x, y), z) => Logic::And(Logic::Or(x, z), Logic::Or(y, z))
  Logic::Or(x, Logic::And(y, z)) => Logic::And(Logic::Or(x, y), Logic::Or(x, z))
}
```

之后我们可以给 过程式/函数式 的部分加上简单类型系统，
类型参数的处理原则和 clause 一样。

也应该暂时删除 term rewrite 的功能，
让语言首先成为一个 pure logic programming + imperative/functional shell。
