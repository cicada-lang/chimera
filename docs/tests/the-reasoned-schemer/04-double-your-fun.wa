import { Null, Cons, Car, Cdr } from "./02-teaching-old-toys-new-tricks.wa"

Append [l, t, out]
------------------ {
  Null l
  unify out = t
}

Append [l, t, out]
------------------ {
  Cdr [l, d]
  Append [d, t, res]
  Car [l, a]
  Cons [a, res, out]
}

query q {
  Append [[1, 2, 3], [4, 5], q]
}

query q {
  Append [[], [4, 5], q]
}

query q {
  Append [1, [4, 5], q]
}

query q {
  Append [[4, 5], 1, q]
}

query x limit 6 {
  Append [x, y, z]
}

query (x, y, z) limit 6 {
  Append [x, y, z]
}

query x {
  Append [
    ["cake"],
    ["tastes", "yummy"],
    x,
  ]
}

query x {
  Append [
    ["cake", "&", "ice", y],
    ["tastes", "yummy"],
    x,
  ]
}

query x {
  Append [
    ["cake", "&", "ice", "cream"],
    y,
    x,
  ]
}

query x limit 1 {
  Append [
    ["cake", "&", "ice", ...y],
    ["d", "t"],
    x,
  ]
}

query x limit 5 {
  Append [
    ["cake", "&", "ice", ...y],
    ["d", "t"],
    x,
  ]
}

query y limit 5 {
  Append [
    ["cake", "&", "ice", ...y],
    ["d", "t"],
    x,
  ]
}

query x limit 5 {
  Append [
    ["cake", "&", "ice", ...y],
    ["d", "t", ...y],
    x,
  ]
}

query x {
  Append [
    ["cake", "&", "ice", "cream"],
    ["d", "t", ...z],
    x,
  ]
}

query (x, y) {
  Append [
    x,
    y,
    ["cake", "&", "ice", "d", "t"],
  ]
}

Unwrap [x, out]
--------------- {
  unify x = out
}

Unwrap [x, out]
--------------- {
  Car [x, a]
  Unwrap [a, out]
}

query x {
  Unwrap [
    [[["pizza"]]],
    x,
  ]
}

query x limit 3 {
  Unwrap [
    x,
    "pizza",
  ]
}

query x limit 3 {
  Unwrap [
    [[x]],
    "pizza",
  ]
}
