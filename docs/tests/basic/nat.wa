Nat "zero"

Nat { prev }
------------ {
  Nat prev
}

Zero "zero"

One { prev }
------------ {
  Zero prev
}

Two { prev }
------------ {
  One prev
}

Add ["zero", y, y]

Add [{ prev }, y, { prev: result_prev } ]
---------------------------------------- {
  Add [prev, y, result_prev]
}

query z {
  One x
  One y
  Add [x, y, z]
}

query (x, y, z) limit 1 {
  Add [x, y, z]
}

query (x, y, z) limit 3 {
  Add [x, y, z]
}

query (x, y) {
  Two z
  Add [x, y, z]
}

Mul ["zero", y, "zero"]

Mul [{ prev }, "zero", "zero"]

Mul [{ prev: prev_x }, { prev: prev_y } , result]
---------------------------- {
  Add [{ prev: prev_y }, z, result]
  Mul [prev_x, { prev: prev_y }, z]
}

query z {
  Two x
  Two y
  Mul [x, y, z]
}

query (x, y) debug {
  Two z
  Mul [x, y, z]
}
