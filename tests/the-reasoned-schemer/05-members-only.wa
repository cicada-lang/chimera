import { Null, Cons, Car, Cdr } from "./02-teaching-old-toys-new-tricks.wa"

Mem [x, l, out]
--------------- {
  Car [l, x]
  out = l
}

Mem [x, l, out]
--------------- {
  Cdr [l, d]
  Mem [x, d, out]
}

query x {
  Mem [
    "fig",
    ["roll", "okra", "fig", "beet", "roll", "pea"],
    x,
  ]
}

query x {
  Mem [
    "fig",
    ["roll", "okra", "beet", "beet", "roll", "pea"],
    x,
  ]
}

query x {
  Mem ["fig", ["pea"], ["pea"]]
}

query out {
  Mem ["fig", ["fig"], out]
}

query out {
  Mem ["fig", ["fig", "pea"], out]
}

query x {
  Mem [
    x,
    ["roll", "okra", "fig", "beet", "roll", "pea"],
    ["fig", "beet", "roll", "pea"],
  ]
}

query x {
  Mem ["fig", ["fig", "pea"], ["pea", x]]
}

query x {
  Mem ["fig", ["fig", "pea"], [x, "pea"]]
}

query out {
  Mem ["fig", ["fig", "fig", "pea"], out]
}

query out {
  Mem ["fig", ["a", x, "c", "fig", "e"], out]
}

query [x, y] limit 5 {
  Mem ["fig", ["fig", "d", "fig", "e", ...y], x]
}

Rember [x, l, out]
------------------ {
  Null l
  Null out
}

Rember [x, l, out]
------------------ {
  Car [l, x]
  Cdr [l, out]
}

Rember [x, l, out]
------------------ {
  Cdr [l, d]
  Rember [x, d, res]
  Car [l, a]
  Cons [a, res, out]
}

query out {
  Rember ["pea", ["a", "b", "pea", "d", "pea", "e"], out]
}

query out {
  Rember ["pea", ["pea"], out]
}

query out {
  Rember ["pea", ["pea", "pea"], out]
}

query out {
  Rember ["pea", ["pea", "pea", "pea"], out]
}

query out {
  Rember [y, ["a", "b", y, "d", z, "e"], out]
}

query [y, z] {
  Rember [y, [y, "d", z, "e"], [y, "d", "e"]]
}

query [y, z, w, out] limit 4 {
  Rember [y, [z, ...w], out]
}

query [y, z, w, out] limit 5 {
  Rember [y, [z, ...w], out]
}
