import { Succeed, Fail } from "./01-playthings.wa"
import { Null, Cons, Car, Cdr } from "./02-teaching-old-toys-new-tricks.wa"

List l
----------- {
  Null l
}

List l
------------- {
  Cdr [l, d]
  List d
}

query x {
  List ["a", "b", x, "d"]
}

query x limit 1 {
  List ["a", "b", ...x]
}

query x limit 5 {
  List ["a", "b", ...x]
}

ListOfLists l
------------- {
  Null l
}

ListOfLists l
------------- {
  Car [l, a]
  List a
  Cdr [l, d]
  ListOfLists d
}

query q {
  ListOfLists [["a", "b"], [x, "c"], ["d", y]]
}

query l limit 1 {
  ListOfLists l
}

query l limit 5 {
  ListOfLists l
}

query x limit 5 {
  ListOfLists [["a", "b"], ...x]
}

query x limit 5 {
  ListOfLists [["a", "b"], ["c", "d"], ...x]
}

Singleton l
----------- {
  unify l = [a]
}

ListOfSingletons l
------------------- {
  Null l
}

ListOfSingletons l
-------------------- {
  Car [l, a]
  Singleton a
  Cdr [l, d]
  ListOfSingletons d
}


query l limit 1 {
  ListOfSingletons l
}

query l limit 5 {
  ListOfSingletons l
}

query r limit 5 {
  ListOfSingletons [["g"], ["e", ...w], [x, ...y], ...z]
  unify r = [w, [x, ...y], z]
}

query out limit 5 {
  unify [["g"], ["e", ...w], [x, ...y], ...z] = out
  ListOfSingletons out
}

// NOTE By "The Law of Fail", we can drop the following clause.

// Member [x, l]
// ------------- {
//   Null l
//   Fail []
// }

// Member [x, l]
// ------------- {
//   Car [l, a]
//   unify x = a
// }

Member [x, l]
------------- {
  Car [l, x]
}

Member [x, l]
------------- {
  Cdr [l, d]
  Member [x, d]
}

query q {
  Member ["olive", ["virgin", "olive", "oil"]]
}

query x {
  Member [x, ["virgin", "olive", "oil"]]
}

query x {
  Member [x, ["hummus", "with", "pita"]]
}

query x {
  Member [x, ["pear", "grape", ..."peaches"]]
}

query x {
  Member ["e", ["pasta", x, "fagioli"]]
}

query x {
  Member ["e", ["pasta", "e", x, "fagioli"]]
}

query x {
  Member ["e", ["pasta", x, "e", "fagioli"]]
}

query (x, y) {
  Member ["e", ["pasta", x, "fagioli", y]]
}

query q {
  unify ["pasta", x, "fagioli", y] = q
  Member ["e", q]
}

query l limit 5 {
  Member ["tofu", l]
}

ProperMember [x, l]
------------------- {
  Car [l, x]
  Cdr [l, d]
  List d
}

ProperMember [x, l]
------------------- {
  Cdr [l, d]
  ProperMember [x, d]
}

query l limit 5 {
  ProperMember ["tofu", l]
}

query l limit 12 {
  ProperMember ["tofu", l]
}
