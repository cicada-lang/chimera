Car [p, a]
---------- {
  [a, ...d] = p
}

query q {
  Car [["a", "c", "o", "r", "n"], q]
}

query q {
  Car [["a", "c", "o", "r", "n"], "a"]
}

query r {
  Car [[r, ...y], x]
  "pear" = x
}

query r {
  Car [["grape", "raisin", "pear"], x]
  Car [[["a"], ["b"], ["c"]], y]
  [x, ...y] = r
}

Cdr [p, d]
---------- {
  [a, ...d] = p
}

query r {
  Cdr [["a", "c", "o", "r", "n"], v]
  Cdr [v, w]
  Car [w, r]
}

query r {
  Cdr [["grape", "raisin", "pear"], x]
  Car [[["a"], ["b"], ["c"]], y]
  [x, ...y] = r
}

query q {
  Cdr [["a", "c", "o", "r", "n"], ["c", "o", "r", "n"]]
}

query x {
  Cdr [["c", "o", "r", "n"], [x, "r", "n"]]
}

query l {
  Cdr [l, ["c", "o", "r", "n"]]
  Car [l, x]
  x = "a"
}

// Cons [a, d, p]
// --------------- {
//   [a, ...d] = p
// }

Cons [a, d, p]
--------------- {
  Car [p, a]
  Cdr [p, d]
}

query l {
  Cons [["a", "b", "c"], ["d", "e"], l]
}

query x {
  Cons [x, ["a", "b", "c"], ["d", "a", "b", "c"]]
}

query r {
  ["e", "a", "d", x] = r
  Cons [y, ["a", z, "c"], r]
}

query x {
  Cons [x, ["a", x, "c"], ["d", "a", x, "c"]]
}

query l {
  ["d", "a", x, "c"] = l
  Cons [x, ["a", x, "c"], l]
}

query l {
  Cons [x, ["a", x, "c"], l]
  ["d", "a", x, "c"] = l
}

query l {
  Cons [w, ["n", "u", "s"], t]
  Cdr [l, t]
  Car [l, x]
  "b" = x
  Cdr [l, d]
  Car [d, y]
  "o" = y
}

Null x
---------- {
  [] = x
}

query q {
  Null ["grape", "raisin", "pear"]
}

query q {
  Null []
}

query x {
  Null x
}

query r {
  [x, y, "salad"] = r
}

Pair p
------ {
  Cons [a, d, p]
}

query q {
  Pair [q, ...q]
}

query q {
  Pair []
}

query q {
  Pair "pair"
}

query x {
  Pair x
}

query r {
  Pair [r]
}

// Singleton l
// ----------- {
//   Pair l
//   Cdr [l, d]
//   Null d
// }

Singleton l
----------- {
  Cdr [l, d]
  Null d
}

query r {
  Singleton ["tofu"]
}

query r {
  Singleton [["tofu"]]
}

query r {
  Singleton "tofu"
}

query r {
  Singleton ["e", "tofu"]
}

query r {
  Singleton []
}

query r {
  Singleton ["e", ..."tofu"]
}
